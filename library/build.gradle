plugins {
  alias(libs.plugins.android.library)
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

def artifactName = rootProject.name
group rootProject.group
version rootProject.version

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(libs.versions.java.get().toInteger())
  }
}

android {
  namespace 'io.michaelrocks.libphonenumber.android'

  defaultConfig {
    compileSdk libs.versions.compileSdk.get().toInteger()
    buildToolsVersion libs.versions.buildTools.get()

    minSdk libs.versions.minSdk.get().toInteger()
    targetSdk libs.versions.targetSdk.get().toInteger()
  }

  compileOptions {
    sourceCompatibility = JavaVersion.toVersion(libs.versions.javaLibrary.get())
    targetCompatibility = JavaVersion.toVersion(libs.versions.javaLibrary.get())
    coreLibraryDesugaringEnabled false
  }

    buildTypes {
    release {
      minifyEnabled false
    }
  }

  variantFilter { variant ->
    if (variant.buildType.name != 'release') {
      variant.setIgnore(true)
    }
  }


  publishing {
    singleVariant('release') {
      withSourcesJar()
      withJavadocJar()
    }
  }
  lint {
    abortOnError false
  }
}

dependencies {
  testImplementation libs.junit
  testImplementation libs.mockito.core
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
        artifactId artifactName
        pom {
          name = 'libphonenumber-android'
          description = 'An Android port of Google\'s libphonenumber.'
          inceptionYear = '2016'
          url = 'https://github.com/michaelrocks/libphonenumber-android'
          packaging = 'aar'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          developers {
            developer {
              id = 'MichaelRocks'
              name = 'Michael Rozumyanskiy'
              email = 'michael.rozumyanskiy@gmail.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com/michaelrocks/libphonenumber-android.git'
            developerConnection = 'scm:git:ssh://git@github.com/michaelrocks/libphonenumber-android.git'
            url = 'https://github.com/michaelrocks/libphonenumber-android'
          }
        }
      }
    }

    repositories {
      if (project.hasProperty('mavenCentralRepositoryUsername') && project.hasProperty('mavenCentralRepositoryPassword')) {
        maven {
          name 'Sonatype'
          url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
          credentials {
            username mavenCentralRepositoryUsername
            password mavenCentralRepositoryPassword
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications.release
  }
}
